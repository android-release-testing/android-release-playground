apply from: "$rootDir/gradle/versioning.gradle"

apply plugin: 'com.android.application'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 33

        applicationId "com.monzo.releases"

        versionCode generateVersionCode()
        versionName generateVersionName()
    }

    lintOptions {
        // This prevents lintVital running every time we build a release variant.
        // It does *not* prevent us explicitly running lint on a release variant.
        checkReleaseBuilds false
    }

    signingConfigs {
        debug {
            storeFile file("$rootDir/debug.jks")
            storePassword 'abcdef'
            keyAlias 'monzo_debug'
            keyPassword 'abcdef'
        }

        release {
            // Our release process builds unsigned release APKs (using the 'unsigned' Gradle property) and
            // performs signing on the backend.
            // These properties exist so that we can build a signed release APK locally in an emergency.
            def keystore = findProperty('keystore') ?: 'default'
            def alias = findProperty('alias') ?: 'default'
            def password = findProperty('password') ?: 'default'

            storeFile rootProject.file(keystore)
            storePassword password
            keyAlias alias
            keyPassword password
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug

            minifyEnabled project.hasProperty('postprocessingEnabled') ? postprocessingEnabled.toBoolean() : false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }

        release {
            signingConfig = project.hasProperty('unsigned') ? null : signingConfigs.release

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
}
