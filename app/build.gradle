apply from: "$rootDir/gradle/versioning.gradle"

apply plugin: 'com.android.application'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 29

        applicationId "com.monzo.releases"

        versionCode generateVersionCode()
        versionName generateVersionName()
    }

    lintOptions {
        // This prevents lintVital running every time we build a release variant.
        // It does *not* prevent us explicitly running lint on a release variant.
        checkReleaseBuilds false
    }

    signingConfigs {
        debug {
            storeFile file("$rootDir/debug.jks")
            storePassword 'abcdef'
            keyAlias 'monzo_debug'
            keyPassword 'abcdef'
        }

        release {
            storeFile file("$rootDir/release.jks")
            storePassword 'uvwxyz'
            keyAlias 'monzo_release'
            keyPassword 'uvwxyz'
        }

        upload {
            storeFile file("$rootDir/upload.jks")
            storePassword '123456'
            keyAlias 'monzo_upload'
            keyPassword '123456'
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug

            minifyEnabled project.hasProperty('postprocessingEnabled') ? postprocessingEnabled.toBoolean() : false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }

        release {
            def signing = findProperty('signing') ?: 'release'
            if (signing == 'unsigned') {
                signingConfig = null
            } else if (signing == 'upload') {
                signingConfig = signingConfigs.upload
            } else {
                signingConfig = signingConfigs.release
            }

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
}
