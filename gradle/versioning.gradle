def generateVersionCode() {
    def splitVersion = rootProject.file('versioning/version').text.trim().split("\\.")
    def major = splitVersion[0].toInteger()
    def minor = splitVersion[1].toInteger()
    def patch = splitVersion[2].toInteger()
    def rc = rootProject.file('versioning/rc').text.trim().toInteger()

    def build
    if (rc > 0) {
        build = rc + 39
    } else {
        def nightly = rootProject.file('versioning/nightly').text.trim().toInteger()
        build = nightly
    }

    if (major < 0 || major > 99) {
        throw new IllegalArgumentException("major version is very large - are you sure you wanted to set it to this?")
    }
    if (minor < 0 || minor > 99) {
        throw new IllegalArgumentException("minor version is out of range")
    }
    if (patch < 0 || patch > 99) {
        throw new IllegalArgumentException("patch version is out of range")
    }
    if (build < 0 || build > 99) {
        throw new IllegalArgumentException("build version is out of range")
    }

    return major * 1_000_000 + minor * 10_000 + patch * 100 + build
}

def generateVersionName() {
    def splitVersion = rootProject.file('versioning/version').text.trim().split("\\.")
    def major = splitVersion[0].toInteger()
    def minor = splitVersion[1].toInteger()
    def patch = splitVersion[2].toInteger()
    def rc = rootProject.file('versioning/rc').text.trim().toInteger()

    if (rc > 0 && rc < 10) {
        // RC build on a release branch
        return "${major}.${minor}.${patch}-rc${rc}"
    }

    if (rc == 10) {
        // Final build on a release branch
        return "${major}.${minor}.${patch}"
    }

    def gitBranch = gitBranch()
    if (gitBranch == "master") {
        def nightlyTag = gitTags().find { it.startsWith("nightly-") }
        if (nightlyTag != null) {
            // Nightly build on master
            return "${major}.${minor}.${patch}-${nightlyTag}"
        }
    }

    // Any other build
    def gitSha = gitSha()
    return "${major}.${minor}.${patch}-${gitBranch}-${gitSha}"
}

ext {
    generateVersionCode = this.&generateVersionCode
    generateVersionName = this.&generateVersionName
}

def gitBranch() {
    def cmd = 'git rev-parse --abbrev-ref HEAD'.execute([], project.rootDir)
    cmd.waitFor()
    if (cmd.exitValue() != 0) {
        throw new RuntimeException(cmd.errorStream.text)
    }

    return cmd.text.trim()
}

def gitSha() {
    def cmd = 'git rev-parse --short HEAD'.execute([], project.rootDir)
    cmd.waitFor()
    if (cmd.exitValue() != 0) {
        throw new RuntimeException(cmd.errorStream.text)
    }

    return cmd.text.trim()
}

def gitTags() {
    def cmd = 'git tag --points-at HEAD'.execute([], project.rootDir)
    cmd.waitFor()
    if (cmd.exitValue() != 0) {
        throw new RuntimeException(cmd.errorStream.text)
    }

    return cmd.text.trim().split('\n')
}
